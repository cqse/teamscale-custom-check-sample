import org.gradle.plugins.ide.eclipse.model.AbstractClasspathEntry
import org.gradle.plugins.ide.eclipse.model.Library
import org.gradle.plugins.ide.eclipse.model.internal.FileReferenceFactory

plugins {
	id 'java-library'
	id 'eclipse'
	id 'idea'
}

ext {
	teamscaleVersion = '5.9.0-rc3'
}

test {
	useJUnitPlatform()

	testLogging {
		// It's sometimes useful to see the stdout during testing
		showStandardStreams = true
		exceptionFormat = 'full'
	}
}

repositories {
	mavenCentral()

	maven {
		url "https://share.cqse.eu/svn/artifacts/maven"
	}
}

dependencies {
	implementation platform("com.teamscale:teamscale-bom:$teamscaleVersion")

	implementation 'com.teamscale:teamscale-check-api'
	implementation 'com.teamscale:teamscale-commons'
	implementation 'com.teamscale:teamscale-lib-commons'
	compile 'org.apache.logging.log4j:log4j-api:2.12.1'

	testImplementation 'com.teamscale:teamscale-check-api-test-fixtures'
}

eclipse {
	classpath {
		defaultOutputDir = file('classes')

		file.whenMerged {
			entries = entries.collect {
				if (it in AbstractClasspathEntry) {
					String scope = it.entryAttributes['gradle_scope']

					// Gradle by default uses bin/<configuration> as output dir
					// For now we need to have all our main code in 'classes' -> default
					if (it.kind == 'src' && it.toString().contains('output') && scope == 'main') {
						it.output = null
					}

					// Workaround for https://github.com/gradle/gradle/issues/4802
					if (scope == 'test') {
						it.entryAttributes['test'] = true
					}

					// Workaround to make eclipse accept java files in test-data resource directories
					if (it.kind == 'src' && it.path == 'test-data') {
						Library library = new Library(new FileReferenceFactory().fromPath(it.path))
						library.entryAttributes['test'] = true
						return library
					}
				}
				return it
			}
		}
	}
}
